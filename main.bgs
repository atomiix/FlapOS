export const HANDLE_MOTOR_RUN = 0
export const HANDLE_MOTOR_STOP = 1
export const HANDLE_RESET_PUSH = 2
export const HANDLE_RECONNECT = 3
export const HANDLE_STATUS_NOTIFICATION = 4
export const HANDLE_SERVER_TIMEOUT = 5

export const APP_MODE_CONFIG = 0
export const APP_MODE_AP = 1
export const APP_MODE_STA = 2
export const APP_MODE_CLI = 3

export const HTTP_GET = 0
export const HTTP_POST = 2

export dim request_data(255)
export dim request_data_len
export dim request_method
export dim request_resource(255)
export dim request_resource_len
export dim response(255)
export dim response_len
export dim result
export dim flash_result_len
export dim flash_result_data(255)
export dim port
export dim data

const VERSION() = "1.0.0"
const WIFI_MODE_STA =  1
const WIFI_MODE_AP = 2

const RESET_DELAY = 3000
const RECONNECT_DELAY = 5000

dim info_version_output(127)
dim mode

import "leds.bgs"
import "motors.bgs"
import "config.bgs"
import "server.bgs"
import "client.bgs"

procedure show_info()
    info_version_output(0:26) = "\r\n******* FlapOS *******\r\n"
    info_version_output(26:15) = "[Info]Version: "
    memcpy(info_version_output(41), VERSION(0), 5)
    info_version_output(46:43) = "\r\n[Info]https://github.com/atomiix/FlapOS\r\n"
    call endpoint_send(0, 89, info_version_output(0:89))
end

procedure do_reset()
    call endpoint_send(0, 19, "[DBG]Resetting...\r\n")
    call flash_ps_save(FLASH_PS_KEY_APP_MODE, 1, APP_MODE_CONFIG)
    call system_reset(0)
end

procedure reset_after_delay()
    call hardware_io_port_read(3, $0001)(result, port, data) # read reset button
    if data = 0 then
        call do_reset()
    end if
end

procedure connect_to_ssid()
    call endpoint_send(0, 27, "[DBG]conneting to ssid...\r\n")
    call tcpip_configure(0, 0, 0, 1)

    call flash_ps_load(FLASH_PS_KEY_CLIENT_PW)(result, flash_result_len, flash_result_data(0:flash_result_len))
    if result = 0 then
        call sme_set_password(flash_result_len, flash_result_data(0:flash_result_len))
    end if

    call flash_ps_load(FLASH_PS_KEY_CLIENT_SSID)(result, flash_result_len, flash_result_data(0:flash_result_len))
    if result = 0 then
        call sme_connect_ssid(flash_result_len, flash_result_data(0:flash_result_len))
    else
        call endpoint_send(0, 20, "[DBG]no ssid found\r\n")
    end if
end

procedure try_reconnect()
    call start_status_notification(LED_ORANGE, INTERVAL_ERROR)
    call hardware_set_soft_timer(RECONNECT_DELAY, HANDLE_RECONNECT, 1)
end

event system_boot(major, minor, patch, build, bootloader_version, tcpip_version, hw)
    call show_info()
    call init_motors()
    call hardware_io_port_config_direction(1, $b800, $0000) # set leds and 1 motor as outputs
    call hardware_io_port_write(1, $b000, $b000) # leds off

    call hardware_io_port_config_direction(3, $06f0, $0000) # motors as output
    call hardware_io_port_config_direction(4, $007f, $007f) # switches as input

    call hardware_external_interrupt_config($11, $0) # attach interrupt on buttons

    call flash_ps_load(FLASH_PS_KEY_APP_MODE)(result, flash_result_len, flash_result_data(0:flash_result_len))
    mode = flash_result_data(0:1)

    if result != 0 || mode > 3 || mode < 1 then
        mode = APP_MODE_CONFIG
    end if

    if mode = APP_MODE_CONFIG || mode = APP_MODE_AP then
        call sme_set_operating_mode(WIFI_MODE_AP)
    else
        call sme_set_operating_mode(WIFI_MODE_STA)
    end if

    call sme_wifi_on()
end

event hardware_soft_timer(handle)
    if handle = HANDLE_MOTOR_RUN then
        call handle_motor_run()
    end if
    if handle = HANDLE_MOTOR_STOP then
        call handle_motors_stop()
    end if
    if handle = HANDLE_RESET_PUSH then
        call reset_after_delay()
    end if
    if handle = HANDLE_RECONNECT then
        call sme_wifi_off()
    end if
    if handle = HANDLE_STATUS_NOTIFICATION then
        call next_status_notification()
    end if
    if handle = HANDLE_SERVER_TIMEOUT then
        call server_timeout()
    end if
end

event sme_wifi_is_on(state)
    if mode = APP_MODE_CLI || mode = APP_MODE_STA then
        call start_status_notification(LED_BLUE, INTERVAL_CONNECTING)
        call connect_to_ssid()
    end if
    if mode = APP_MODE_AP then
        call start_status_notification(LED_BLUE, INTERVAL_CONNECTING)
        call start_ap_mode()
    end if
    if mode = APP_MODE_CONFIG then
        call start_status_notification(LED_ORANGE, INTERVAL_CONNECTING)
        call sme_start_ssid_scan(0, "")
    end if
end

event sme_wifi_is_off(state)
    call sme_wifi_on()
end

event sme_ap_mode_started(hw_interface)
    if mode = APP_MODE_CONFIG then
        call config_sme_ap_mode_started(hw_interface)
    end if
    if mode = APP_MODE_AP then
        call server_sme_ap_mode_started(hw_interface)
    end if
end

event sme_connected(status, hw_interface, bssid)
    call endpoint_send(0, 20, "[DBG]sme connected\r\n")
    call start_status_notification(LED_BLUE, INTERVAL_CONNECTED)
    call flash_ps_save(FLASH_PS_KEY_SETUP_SUCCESS, 1, 1)
    if mode = APP_MODE_STA then
        call server_sme_connected(status, hw_interface, bssid(:))
    end if
    if mode = APP_MODE_CLI then
        call cli_sme_connected(status, hw_interface, bssid(:))
    end if
end

event sme_connect_failed(reason, hw_interface)
    call endpoint_send(0 , 25, "[DBG]sme_connect_failed\r\n")
    call flash_ps_load(FLASH_PS_KEY_SETUP_SUCCESS)(result, flash_result_len, flash_result_data(0:flash_result_len))
    if flash_result_data(0:1) = 1 then
        call try_reconnect()
    else
        call do_reset()
    end if
end

event sme_disconnected(reason, hw_interface)
    call endpoint_send(0 , 23, "[DBG]sme_disconnected\r\n")
    call try_reconnect()
end

event tcpip_dns_configuration(index, address)
    call endpoint_send(0, 30, "[DBG]tcpip_dns_configuration\r\n")
    if mode = APP_MODE_CLI then
        call cli_tcpip_dns_configuration(index, address)
    end if
    if mode = APP_MODE_STA then
        call server_tcpip_dns_configuration(index, address)
    end if
end

event tcpip_dns_gethostbyname_result(dns_result, address, name_len, name_data)
    call cli_tcpip_dns_gethostbyname_result(dns_result, address, name_len, name_data(0:name_len))
end

event https_api_request(request, method, resource_len, resource_data)
    call endpoint_send(0, 18, "[DBG]api request\r\n")
    request_method = method
    request_resource(0:resource_len) = resource_data(0:resource_len)
    request_resource_len = resource_len
    request_data_len = 0
end

event https_api_request_data(request, data_len, data_data)
    call endpoint_send(0, 23, "[DBG]api request data\r\n")
    memcpy(request_data(request_data_len), data_data(0), data_len)
    request_data_len = request_data_len + data_len
end

event https_api_request_finished(request)
    call endpoint_send(0, 23, "[DBG]request finished\r\n")
    if mode = APP_MODE_CONFIG then
        call config_https_api_request_finished(request)
    end if
    if mode = APP_MODE_STA || mode = APP_MODE_AP then
        call server_https_api_request_finished(request)
    end if
end

event endpoint_status(endpoint, type, streaming, destination, active)
    call cli_endpoint_status(endpoint, type, streaming, destination, active)
end

event endpoint_data(endpoint, data_len, data_data)
    call cli_endpoint_data(endpoint, data_len, data_data(0:data_len))
end

event hardware_external_interrupt(irq, interrupt)
    if irq = 0 then
        call endpoint_send(0, 20, "[DBG]Reset pressed\r\n")
        call hardware_set_soft_timer(RESET_DELAY, HANDLE_RESET_PUSH, 1)
    else
        call endpoint_send(0, 18, "[DBG]WPS pressed\r\n")
    end if
end

event sme_scanned(status)
    call config_sme_scanned(status)
end

event sme_scan_sort_result(bssid, channel, rssi, snr, secure, ssid_len, ssid_data)
    # special case here, bssid are ssid_data are both a buffer but we only pass 1 buffer to a procedure
    # as we don’t care about the bssid in this case, it’s fine for now.
    call config_sme_scan_sort_result(channel, rssi, snr, secure, ssid_len, ssid_data(0:ssid_len))
end

event sme_scan_sort_finished()
    call config_sme_scan_sort_finished()
end